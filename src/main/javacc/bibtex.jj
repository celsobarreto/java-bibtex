/*
 * Copyright (c) 2012 University of Tartu
 */
PARSER_BEGIN(BibTeXParser)
package org.jbibtex;

import java.io.*;
import java.util.*;

public class BibTeXParser {

	private BibTeXDatabase database = null;

	private BibTeXObjectFactory objectFactory = BibTeXObjectFactory.DEFAULT;

	private KeyFactory keyFactory = KeyFactory.DEFAULT;

	private ValueFactory valueFactory = ValueFactory.DEFAULT;

	private boolean initialized = false;

	private KeyMap<BibTeXString> macros = new KeyMap<BibTeXString>();


	public BibTeXParser(){
		this(new StringReader(""));
	}

	public BibTeXDatabase parse(Reader reader) throws IOException, ParseException {
		ReInit(reader);

		return Database();
	}

	/**
	 * Checks the <code>string</code> argument before constructing a new {@link ReferenceValue} instance.
	 * The default behaviour is to prohibit unresolved references.
	 *
	 * @throws ObjectResolutionException If the <code>string</code> is <code>null</code>.
	 *
	 * @see BibTeXDatabase#resolveString(Key)
	 * @see BibTeXParser#getMacros()
	 */
	public void checkStringResolution(Key key, BibTeXString string){

		if(string == null){
			throw new ObjectResolutionException(key);
		}
	}

	private void resolveCrossReferences(){
		Map<Key, BibTeXEntry> entries = getDatabase().getEntries();

		Key crossrefKey = getKeyFactory().createKey("crossref");

		for(BibTeXEntry entry : entries.values()){
			Map<Key, Value> fields = entry.getFields();

			Value value = fields.get(crossrefKey);
			if((value == null) || (value instanceof CrossReferenceValue)){
				continue;
			}

			Key key = getKeyFactory().createKey(value.toUserString());

			BibTeXEntry object = getDatabase().resolveEntry(key);
			checkCrossReferenceResolution(key, object);

			entry.addField(crossrefKey, getValueFactory().createCrossReferenceValue(value, object));
		}
	}

	/**
	 * Checks the <code>entry</code> argument before constructing a new {@link CrossReferenceValue} instance.
	 * The default behaviour is to prohibit unresolved references.
	 *
	 * @throws ObjectResolutionException If the <code>entry</code> is <code>null</code>.
	 *
	 * @see BibTeXDatabase#resolveEntry(Key)
	 */
	public void checkCrossReferenceResolution(Key key, BibTeXEntry entry){

		if(entry == null){
			throw new ObjectResolutionException(key);
		}
	}

	public BibTeXDatabase getDatabase(){
		return this.database;
	}

	private void setDatabase(BibTeXDatabase database){
		this.database = database;
	}

	public BibTeXObjectFactory getObjectFactory(){
		return this.objectFactory;
	}

	public void setObjectFactory(BibTeXObjectFactory objectFactory){

		if(objectFactory == null){
			throw new IllegalArgumentException();
		}

		this.objectFactory = objectFactory;
	}

	public KeyFactory getKeyFactory(){
		return this.keyFactory;
	}

	public void setKeyFactory(KeyFactory keyFactory){

		if(keyFactory == null){
			throw new IllegalArgumentException();
		}

		this.keyFactory = keyFactory;
	}

	public ValueFactory getValueFactory(){
		return this.valueFactory;
	}

	public void setValueFactory(ValueFactory valueFactory){

		if(valueFactory == null){
			throw new IllegalArgumentException();
		}

		this.valueFactory = valueFactory;
	}

	protected void initMacros(){

		if(this.initialized){
			return;
		}

		addMacro("jan", "January");
		addMacro("feb", "February");
		addMacro("mar", "March");
		addMacro("apr", "April");
		addMacro("may", "May");
		addMacro("jun", "June");
		addMacro("jul", "July");
		addMacro("aug", "August");
		addMacro("sep", "September");
		addMacro("oct", "October");
		addMacro("nov", "November");
		addMacro("dec", "December");

		this.initialized = true;
	}

	public KeyMap<BibTeXString> getMacros(){
		return this.macros;
	}

	protected void addMacro(String key, String value){
		addMacro(getObjectFactory().createString(getKeyFactory().createKey(key), getValueFactory().createStringValue(value, StringValue.Style.BRACED)));
	}

	public void addMacro(BibTeXString macro){
		this.macros.put(macro.getKey(), macro);
	}

	public void removeMacro(BibTeXString macro){
		this.macros.remove(macro.getKey());
	}


	static
	private class Field {

		private Key key = null;

		private Value value = null;


		private Field(Key key, Value value){
			setKey(key);
			setValue(value);
		}

		public Key getKey(){
			return this.key;
		}

		private void setKey(Key key){
			this.key = key;
		}

		public Value getValue(){
			return this.value;
		}

		private void setValue(Value value){
			this.value = value;
		}
	}
}
PARSER_END(BibTeXParser)

<DEFAULT>
MORE :
{
	"%" : IN_INLINE_COMMENT
}

<IN_INLINE_COMMENT>
SPECIAL_TOKEN :
{
	< INLINE_COMMENT : ( ~["\r", "\n"] )* ("\r" | "\n" | "\r\n")? > : DEFAULT
}

<IN_INLINE_COMMENT>
MORE :
{
	< ~[] >
}

<IN_LITERAL>
TOKEN :
{
	< TEXT : ~[] >
}

<DEFAULT>
SKIP :
{
	"\t"
	|
	"\n"
	|
	"\f"
	|
	"\r"
	|
	" "
}

<DEFAULT>
TOKEN :
{
	< AT : "@" >
	|
	< COMMA : "," >
	|
	< EQUALS : "=" >
	|
	< HASH : "#" >
	|
	< LBRACE : "{">
	|
	< LPAREN : "(" >
	|
	< QUOTE : "\"" >
	|
	< RBRACE : "}" >
	|
	< RPAREN : ")" > 
}

<DEFAULT>
TOKEN :
{
	< COMMENT : ( ["c", "C"] ["o", "O"] ["m", "M"] ["m", "M"] ["e", "E"] ["n", "N"] ["t", "T"] ) >
	|
	< INCLUDE : ( ["i", "I"] ["n", "N"] ["c", "C"] ["l", "L"] ["u", "U"] ["d", "D"] ["e", "E"] ) >
	|
	< PREAMBLE : ( ["p", "P"] ["r", "R"] ["e", "E"] ["a", "A"] ["m", "M"] ["b", "B"] ["l", "L"] ["e", "E"] ) >
	|
	< STRING : ( ["s", "S"] ["t", "T"] ["r", "R"] ["i", "I"] ["n", "N"] ["g", "G"] ) >
}

<DEFAULT>
TOKEN :
{
	< NAME : ( ["a"-"z", "A"-"Z"] ( ["a"-"z", "A"-"Z", "0"-"9", "'", "+", "-", ".", "/", ":", "_"] )* ) >
	|
	< DIGITS : ( ["0"-"9"] )+ >
}

private BibTeXDatabase Database() :
{
	BibTeXDatabase database = new BibTeXDatabase();

	BibTeXObject object = null;
}
{
	{
		initMacros();

		setDatabase(database);
	}

	( object = Object() { database.addObject(object); } )* <EOF>

	{
		resolveCrossReferences();

		setDatabase(null);

		return database;
	}
}

private BibTeXObject Object() :
{
	BibTeXObject object;
}
{
	(
		LOOKAHEAD( <AT> <COMMENT> )
		object = Comment()
		|
		LOOKAHEAD( <AT> <INCLUDE> )
		object = Include()
		|
		LOOKAHEAD( <AT> <PREAMBLE> )
		object = Preamble()
		|
		LOOKAHEAD( <AT> <STRING> )
		object = String()
		|
		object = Entry()
	)

	{
		return object;
	}
}

private BibTeXComment Comment() :
{
	String string;
}
{
	<AT> <COMMENT>
	(
		<LBRACE> string = Literal(1, "}")
	)

	{
		return getObjectFactory().createComment(getValueFactory().createStringValue(string, StringValue.Style.BRACED));
	}
}

private BibTeXInclude Include():
{
	String string;
}
{
	<AT> <INCLUDE>
	(
		<LBRACE> string = Literal(1, "}")
	)

	{
		BibTeXDatabase database;

		try {
			Reader reader = new FileReader(string);

			try {
				BibTeXParser parser = new BibTeXParser();

				database = parser.parse(reader);
			} finally {
				reader.close();
			}
		} catch(Exception e){
			throw new ParseException(e.getMessage());
		}

		return getObjectFactory().createInclude(getValueFactory().createStringValue(string, StringValue.Style.BRACED), database);
	}
}

private BibTeXPreamble Preamble() :
{
	Value value;
}
{
	<AT> <PREAMBLE>
	(
		<LPAREN> value = Value() <RPAREN>
		|
		<LBRACE> value = Value() <RBRACE>
	)

	{
		return getObjectFactory().createPreamble(value);
	}
}

private BibTeXString String() :
{
	Field field;
}
{
	<AT> <STRING>
	(
		<LPAREN> field = Assignment() <RPAREN>
		|
		<LBRACE> field = Assignment() <RBRACE>
	)

	{
		return getObjectFactory().createString(field.getKey(), field.getValue());
	}
}

private BibTeXEntry Entry() :
{
	Token type;

	Token key = null;

	List<Field> fields;
}
{
	<AT> type = <NAME>
	(
		<LPAREN> 
		(
			LOOKAHEAD( Identifier() <COMMA> )
			key = Identifier() <COMMA> fields = AssignmentList()
			|
			fields = AssignmentList()
		)
		( <COMMA> )? <RPAREN>
		|
		<LBRACE> 
		(
			LOOKAHEAD( Identifier() <COMMA> )
			key = Identifier() <COMMA> fields = AssignmentList()
			|
			fields = AssignmentList()
		)
		( <COMMA> )? <RBRACE>
	)

	{
		KeyFactory keyFactory = getKeyFactory();

		BibTeXEntry entry = getObjectFactory().createEntry(keyFactory.createKey(type.image), keyFactory.createKey(key != null ? key.image : null));

		for(Field field : fields){
			entry.addField(field.getKey(), field.getValue());
		}

		return entry;
	}
}

private Field Assignment() :
{
	Key key;

	Value value;
}
{
	key = Key() <EQUALS> value = Value()

	{
		return new Field(key, value);
	}
}

private List<Field> AssignmentList() :
{
	List<Field> fields = new ArrayList<Field>();

	Field field;
}
{
	field = Assignment() { fields.add(field); } ( LOOKAHEAD( <COMMA> Name() ) <COMMA> field = Assignment() { fields.add(field); } )*

	{
		return fields;
	}
}

private Token Identifier() :
{
	Token token;
}
{
	(
		token = <DIGITS>
		|
		token = Name()
	)

	{
		return token;
	}
}

private Token Name() :
{
	Token token;
}
{
	(
		token = <COMMENT>
		|
		token = <INCLUDE>
		|
		token = <PREAMBLE>
		|
		token = <STRING>
		|
		token = <NAME>
	)

	{
		return token;
	}
}

private Key Key() :
{
	Token token;
}
{
	token = Name()

	{
		return getKeyFactory().createKey(token.image);
	}
}

private Value Value() :
{
	Value left;

	Value right = null;
}
{
	left = SimpleValue() ( <HASH> right = Value() )?

	{
		if(right != null){
			return getValueFactory().createConcateValue(left, right);
		}

		return left;
	}
}

private Value SimpleValue() :
{
	String string;

	Token token;
}
{
	(
		<LBRACE> string = Literal(1, "}")
		{
			return getValueFactory().createStringValue(string, StringValue.Style.BRACED);
		}
		|
		<QUOTE> string = Literal(0, "\"")
		{
			return getValueFactory().createStringValue(string, StringValue.Style.QUOTED);
		}
		|
		token = Name()
		{
			Key key = getKeyFactory().createKey(token.image);

			BibTeXString object = getDatabase().resolveString(key);
			if(object == null){
				object = getMacros().get(key);
			}

			checkStringResolution(key, object);

			KeyValue value = getValueFactory().createKeyValue(token.image);

			return getValueFactory().createReferenceValue(value, object);
		}
		|
		token = <DIGITS>
		{
			return getValueFactory().createDigitStringValue(token.image);
		}
	)
}

JAVACODE
private String Literal(int braceLevel, String delimiter){
	StringBuffer sb = new StringBuffer(64);

	if(braceLevel < 0){
		throw new IllegalArgumentException();
	}

	token_source.SwitchTo(IN_LITERAL);

	for(Token token = getNextToken(); true; token = getNextToken()){

		if("{".equals(token.image)){
			braceLevel++;
		} else

		if("}".equals(token.image)){
			braceLevel--;
		} // End if

		if(delimiter.equals(token.image) && braceLevel == 0){
			break;
		}

		sb.append(token.image);
	}

	token_source.SwitchTo(DEFAULT);

	return StringUtil.removeIndent(sb.toString());
}